data BST a where
	Empty :: BST a
	Node  :: x: a -> l: BST {a | _v < x} -> r: BST {a | x < _v} -> BST a
                          
termination measure size :: BST a -> {Int | _v >= 0} where
  Empty -> 0
  Node x l r -> size l + size r + 1
  
measure telems :: BST a -> Set a where
  Empty -> []
  Node x l r -> telems l + telems r + [x]
  
data MinPair a where
	MinPair :: x: a -> t: BST {a | x < _v}  -> MinPair a
  
measure min :: MinPair a -> a where
  MinPair x t -> x
  
measure rest :: MinPair a -> BST a where
  MinPair x t -> t  

extractMin :: t: {BST a | size _v > 0} -> {MinPair a | telems t == [min _v] + telems (rest _v)}
extractMin = ??