qualifier {x <= y, x != y}

red   :: {Int | _v == 0}
black :: {Int | _v == 1}

data RBT a where
    Empty :: RBT a
    Node  :: x: a ->
             col: {Int | 0 <= _v && _v <= 1} ->
             left:  { RBT {a | _v < x} | col == 0 ==> color _v == 1 } ->                -- left keys are less than this key; a red node must have black children
             right: { RBT {a | x < _v} | (col == 0 ==> color _v == 1) &&               	-- right keys are greater than this key; a red node must have black children
                                        (blackHeight _v == blackHeight left)} ->        -- blackHeight the same as of left
             RBT a
                      
termination measure size :: RBT a -> {Int | _v >= 0} where
  Empty -> 0
  Node x c l r -> 1 + size l + size r
  
measure color :: RBT a -> {Int | 0 <= _v && _v <= 1} where
  Empty -> 1
  Node x c l r -> c
  
measure elems :: RBT a -> Set a where
  Empty -> []
  Node x c l r -> [x] + elems l + elems r

measure blackHeight :: RBT a -> {Int | _v >= 0} where
  Empty -> 0
  Node x c l r -> c + blackHeight l
  
data WeakRBT a where
    Ok :: x: a ->
             c: {Int | 0 <= _v && _v <= 1} ->
             left:  { RBT {a | _v < x} | c == 0 ==> color _v == 1 } ->
             right: { RBT {a | x < _v} | (c == 0 ==> color _v == 1) &&
                                        (blackHeight _v == blackHeight left)} ->
             WeakRBT a
    Bad :: x: a ->
              lc:    {Int | 0 <= _v  && _v <= 1 } ->
              left:  { RBT {a | _v < x} | color _v == lc} ->
              right: { RBT {a | x < _v } | color _v != lc && blackHeight _v == blackHeight left} ->
              WeakRBT a

termination measure wsize :: WeakRBT a -> {Int | _v >= 0} where
  Ok x c left right -> 1 + size left + size right
  Bad x lc left right -> 1 + size left + size right

measure welems :: WeakRBT a -> Set a where
  Ok x c left right -> [x] + elems left + elems right
  Bad x lc left right -> [x] + elems left + elems right
  
measure wheight :: WeakRBT a -> {Int | _v >= 0} where
  Ok x c left right -> c + blackHeight left
  Bad x lc left right -> blackHeight left

measure isOk :: WeakRBT a -> Bool where
  Ok x c left right -> True
  Bad x lc left right -> False
  
measure wcolor :: WeakRBT a -> {Int | _v >= 0 && _v <= 1} where
  Ok x c left right -> c
  Bad x lc left right -> 0
  
error :: {Int | False} -> a  
  
rotateL :: x: a ->                
            lx: {a | _v < x} ->
            llc: {Int | 0 <= _v && _v <= 1} ->
            ll: {RBT {a | _v < lx} | color _v == llc} ->
            lr: {RBT {a | lx < _v && _v < x} | color _v != llc && blackHeight _v == blackHeight ll} -> 
            r: {RBT {a | x < _v} | blackHeight _v == blackHeight ll} ->
            {WeakRBT a | isOk _v && wcolor _v == 0 && welems _v == [x, lx] + elems ll + elems lr + elems r && wheight _v == blackHeight r + 1 && wsize _v == 2 + size ll + size lr + size r}
rotateL = \x . \lx . \llc . \ll. \lr. \r .
            if llc == 0
              then match ll with
                    Empty -> error 0
                    Node llx llc lll llr -> Ok lx red (Node llx black lll llr) (Node x black lr r)  
              else match lr with
                    Empty -> error 0
                    Node lrx lrc lrl lrr -> Ok lrx red (Node lx black ll lrl) (Node x black lrr r)        
        
rotateR :: x: a ->
            l: RBT {a | _v < x} ->
            rx: {a | x < _v} ->
            rlc: {Int | _v == 0 || _v == 1} ->
            rl: {RBT {a | x < _v && _v < rx} | color _v == rlc && blackHeight _v == blackHeight l} ->
            rr: {RBT {a | rx < _v} | color _v != rlc && blackHeight _v == blackHeight l} ->             
            {WeakRBT a | isOk _v && wcolor _v == 0 && welems _v == [x, rx] + elems l + elems rl + elems rr && wheight _v == blackHeight l + 1 && wsize _v == 2 + size l + size rl + size rr}
rotateR = \x . \l . \rx . \rlc. \rl. \rr .
            if rlc == 0
              then match rl with
                    Empty -> error 0
                    Node rlx rlc rll rlr -> Ok rlx red (Node x black l rll) (Node rx black rlr rr)  
              else match rr with
                    Empty -> error 0
                    Node rrx rrc rrl rrr -> Ok rx red (Node x black l rl) (Node rrx black rrl rrr)
  
balanceL :: y: a ->
              c: {Int | 0 <= _v  && _v <= 1} ->
              l: {WeakRBT {a | _v < y} | c == 0 ==> isOk _v} ->
              r: {RBT {a | _v > y} | (c == 0 ==> color _v == 1) && blackHeight _v == wheight l} ->
              {WeakRBT a | welems _v == [y] + welems l + elems r && 
                            wheight _v == blackHeight r + c && 
                            wsize _v == 1 + wsize l + size r &&
                            (isOk _v || c == 0)}
balanceL = \y . \c . \l . \r .
  match l with
    Ok x1 c1 l1 r1 -> if c == 0 && c1 == 0
                        then Bad y c (Node x1 c1 l1 r1) r
                        else Ok y c (Node x1 c1 l1 r1) r
    Bad lx lc ll lr -> rotateL y lx lc ll lr r                      

balanceR :: x: a ->
              c: {Int | 0 <= _v  && _v <= 1} ->
              l: {RBT {a | _v < x} | c == 0 ==> color _v == 1} ->
              r: {WeakRBT {a | _v > x} | (c == 0 ==> isOk _v) && wheight _v == blackHeight l} ->
              {WeakRBT a | welems _v == [x] + elems l + welems r && 
                            wheight _v == blackHeight l + c && 
                            wsize _v == 1 + size l + wsize r &&
                            (isOk _v || c == 0)}
balanceR = \y . \c . \l . \r .
  match r with
    Ok x1 c1 l1 r1 -> if c == 0 && c1 == 0
                        then Bad y black l (Node x1 c1 l1 r1)
                        else Ok y c l (Node x1 c1 l1 r1)
    Bad rx rc rl rr -> rotateR y l rx rc rl rr
  
ins :: x: a -> t: RBT a -> {WeakRBT a | welems _v == [x] + elems t &&
                                          wheight _v == blackHeight t &&
                                          size t <= wsize _v && wsize _v <= size t + 1 &&
                                          (isOk _v || color t == 0)}
ins = \x . \t . 
        match t with
          Empty -> Ok x red Empty Empty
          Node y c l r -> if x == y
                            then Ok y c l r
                            else if x < y
                                  then balanceL y c (ins x l) r
                                  else balanceR y c l (ins x r)        

mkBlack :: t: WeakRBT a -> {RBT a | elems _v == welems t}
mkBlack = \t . 
  match t with
    Ok x c l r -> Node x c l r
    Bad x lc l r -> Node x black l r

insert :: x: a -> t: RBT a -> {RBT a | elems _v == elems t + [x]}
insert = \x . \t . mkBlack (ins x t)
